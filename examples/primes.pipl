/*

This is a multi-line comment block.

*/

// This is a single-line comment.

import os;
import math;

source icount(start:Num, finish:Num, step:Num = 1) {
	n=start;
	while n <= finish {
		prod n;
		n+=step;
	}
}

predicate Posint(n) {n is Int and n > 0}

source factor(n:Posint) {
	x=n;
	s=n -> math.sqrt;
	verify s;
	2,icount(3,s,2) -> local {
		recompute=false;
		while x%i == 0 {
			prod i;
			x/=i;
			recompute=true;
		}
		if recompute {s=x -> math.sqrt;}
	}
	if x > 1 {prod x;}
}

sink doTakeFirst(x) {
	prod x;
	end;
}

sink doCount(x) {
	prod ?STORE+1;
}

pipe isPrime(n:Posint) {
	factor(n) -> doTakeFirst:firstFactor;
	firstFactor == n;
}

pipe filter(x, y:Bool) {
	if not y {drop;}
	x;
}

sink MAIN {
	start=1 -> os.args -> toInt;
	verify start;
	finish=2 -> os.args -> toInt;
	verify finish;
	if start > finish {end;}
	icount(start,finish) -> @isPrime -> filter -> detour doCount{0}:numPrimes -> doPrint;
	numPrimes -> doPrint;
	prod 0;
}
